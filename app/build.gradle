plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs'
    id 'dev.rikka.tools.refine.gradle-plugin'
    id 'com.google.android.gms.oss-licenses-plugin'
}

//Permission for Columbus to use
rootProject.ext.set("columbusPermission", "com.google.android.columbus.taptap.permission.CONFIGURE_COLUMBUS_GESTURE")

def tagName = '1.2'

android {
    compileSdk 32
    buildToolsVersion "32.0.0"

    defaultConfig {
        applicationId "com.kieronquinn.app.taptap"
        minSdk 24
        targetSdk 32
        versionCode 1200
        versionName tagName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //Broadcast actions
        def BROADCAST_ACTION_START = "${applicationId}.action.START"
        def BROADCAST_ACTION_STOP = "${applicationId}.action.STOP"
        buildConfigField "String", "BROADCAST_ACTION_START", "\"$BROADCAST_ACTION_START\""
        buildConfigField "String", "BROADCAST_ACTION_STOP", "\"$BROADCAST_ACTION_STOP\""
        buildConfigField "String", "TAG_NAME", "\"${tagName}\""
        manifestPlaceholders.broadcast_action_start = BROADCAST_ACTION_START
        manifestPlaceholders.broadcast_action_stop = BROADCAST_ACTION_STOP

    }

    buildTypes {
        release {
            //minify is enabled, but obfuscation is intentionally disabled in the rules
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    lintOptions {
        checkReleaseBuilds true
    }
}

dependencies {

    //AndroidX
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.work:work-runtime-ktx:2.7.1"
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.vectordrawable:vectordrawable-seekable:1.0.0-alpha02"
    implementation "androidx.core:core-splashscreen:1.0.0-beta01"
    implementation "androidx.fragment:fragment-ktx:1.4.1"

    //Lifecycle
    def lifecycleVersion = '2.4.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-service:$lifecycleVersion"

    //Desugaring to allow Java Date on older Android versions
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    //OSS licences screen
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    //Room
    def room_version = "2.4.1"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    //MD3
    implementation 'com.google.android.material:material:1.6.0-alpha02'

    //MonetCompat for Monet backwards compatibility
    implementation 'com.github.KieronQuinn:MonetCompat:0.4.1'

    //Palette for MonetCompat color selection on older Android versions
    implementation "androidx.palette:palette-ktx:1.0.0"

    //Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0'

    //Koin
    implementation "io.insert-koin:koin-android:3.1.5"

    //Retrofit for updates + device spec query (opt-in)
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"

    //Gson for serialization
    implementation 'com.google.code.gson:gson:2.8.9'

    //Columbus module is separate to make it easier to work with
    implementation project(path: ':columbus')

    //Tensorflow, used by Columbus (change in :columbus too)
    implementation 'org.tensorflow:tensorflow-lite-support:0.3.0'

    //Protobuf, used in Columbus
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'

    //System stubs, shouldn't be included in the APK
    compileOnly project(path: ':systemstubs')

    //Bypass hidden API requirements for Shizuku + more
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.2'

    //Tasker library to fire events
    implementation 'com.joaomgcd:taskerpluginlibrary:0.4.2'

    //Shizuku + libsu for shell/root services
    def shizuku_version = '12.1.0'
    implementation "dev.rikka.shizuku:api:$shizuku_version"
    implementation "dev.rikka.shizuku:provider:$shizuku_version"
    implementation "dev.rikka.tools.refine:runtime:$refine_version"

    def libsuVersion = '3.2.1'
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:service:${libsuVersion}"

    //Inline links
    implementation 'me.saket:better-link-movement-method:2.2.0'

    //Picasso for image loading
    implementation 'com.squareup.picasso:picasso:2.71828'

    //Autostarter provides some OEM battery checks + starting settings for disabling them
    implementation 'com.github.judemanutd:autostarter:1.1.0'

    //Markdown support
    implementation "io.noties.markwon:core:4.6.2"

    //Lottie for animations
    implementation 'com.airbnb.android:lottie:4.2.2'

    //Process Phoenix to fully restart the app when needed
    implementation 'com.jakewharton:process-phoenix:2.1.2'

    //Test
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

}